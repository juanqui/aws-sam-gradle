plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.7"
}

repositories {
   jcenter()
}

version = '0.0.3'
group = 'com.github.kaklakariada.aws'

ext {
  awsApiVersion = '1.11.69'
  gradlePluginId = 'com.github.kaklakariada.aws-sam-deploy'
}

sourceSets {
    stest {
        java {
            srcDir file('src/stest/java')
        }
        resources {
            srcDir file('src/stest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    compile "com.amazonaws:aws-java-sdk-s3:$awsApiVersion"
    compile "com.amazonaws:aws-java-sdk-cloudformation:$awsApiVersion"
    compile "com.amazonaws:aws-java-sdk-sts:$awsApiVersion"
    
    testCompile 'junit:junit:4.12'
    stestCompile 'junit:junit:4.12'
    stestCompile gradleTestKit()
}

gradlePlugin {
    testSourceSets sourceSets.stest 
    plugins {
        awsSamDeployPlugin {
            id = gradlePluginId
            implementationClass = 'com.github.kaklakariada.aws.sam.AwsSamDeployPlugin'
        }
    }
}

pluginBundle {
  website = 'https://github.com/kaklakariada/aws-sam-gradle/blob/master/README.md'
  vcsUrl = 'https://github.com/kaklakariada/aws-sam-gradle'
  description = 'Gradle plugin for deploying AWS Serverless Application Models using CloudFormation'
  tags = ['aws', 'serverless', 'serverless application model', 'sam',
          'cloudformation', 'deployment', 'api gateway', 'lambda']

  plugins {
    awsSamDeployPlugin {
      id = gradlePluginId
      displayName = 'AWS serverless deployment plugin'
    }
  }
}

task stest(type: Test) {
    testClassesDir = sourceSets.stest.output.classesDir
    classpath = sourceSets.stest.runtimeClasspath
    testLogging {
        showStandardStreams = true
    }
}
